# T# Documentation

## はじめに

T# は Forth に似てる、Go ʕ◔ϖ◔ʔ で書かれたスタック指向言語です。しかし、私は Forth でプログラミングしたことがあまりないので、実際にはわかりません。

## インストール(ビルド)
```bash
$ git clone https://github.com/Tsharp-lang/Tsharp
$ cd tsharp
$ go build main.go
$ ./main examples/main.tsp
or
$ ./main.exe examples/main.tsp
```

## Table of Contents
<table>
    <tr><td width=33% valign=top>

* [Hello World](#hello-world)
* [Comments](#comments)
* [Variables](#variables)

* [Data types](#data-types)
    * [Typeof](#typeof)

* [Arithmetic](#arithmetic)
* [Block](#block)

* [List](#list)
    * [Append](#append)
    * [Read](#read)
    * [Remove](#remove)
    * [Len](#len)

* [If Statement](#if-statement)

* [For loop](#for-loop)
    * [Loop 100 times](#loop-100-times)
    * [Break](#break)
 
* [Error handling](#error-handling)
* [Assertions](#assertions)
* [Import](#import)

</td><td width=33% valign=top>

* [Built-in Words](#built-in-words)
    * [Dup](#dup)
    * [Drop](#drop)
    * [Over](#over)
    * [Rot](#rot)
    * [Swap](#swap)
    * [Inc](#inc)
    * [Dec](#dec)
    * [Input](#input)
    * [Exit](#exit)
    * [PrintS](#prints)

</td><td valign=top>

* [Examples](#examples)
    * [FizzBuzz](#fizzbuzz)
    * [Factorial](#factorial)
    * [Bubble Sort](#bubble-sort)
    * [Fibonacci](#fibonacci)
        
</td></tr>
</table>

## Hello World
```pascal
"Hello World!" print
```
`print` スタックの一番上の値を出力してスタックから除きます。

## Comments
```python
# comment...
```
コメントはPythonと同じです。複数行のコメントアウトはまだできません、実装は簡単ですが開発者がめんどくさがり屋なのd。。。

## Variables
```pascal
10 -> x

x -> y

y print
```
スタックの一番上の値が変数に入ってスタックから消えます。変数名は矢印の右側に書きます。変数は全てグローバル変数です。いつかスコープつけます。

## Data types
```python
int # 12345
string # "Hello World!"
bool # true false
list # { 1 2 3 4 5 }
type # int string bool type
error # NameError, StackIndexError...
```

### Typeof
```python
"Hello World" typeof print
```
`typeof` スタックの一番上の値のデータ型をスタックに入れます。`typeof`に使われた値はスタックから消えます。

## Arithmetic
```pascal
34 35 + print

100 40 - print

200 5 / print

10 2 * print
```
T# での計算は逆ポーランド記法で書きます。

## Block
```pascal
block main do
    "Hello World!" print
end

call main
```

`block` は他の言語でいう関数です。`block`にはスコープがありません。変数やスタックは全てグローバルスコープです。

## List
```python
{ 1 2 3 4 5 6 7 8 9 10 } print
```
### Append
```python
{ 1 2 3 4 5 6 7 8 9 10 } 11 append
```
### Read
```python
{ 1 2 3 4 5 6 7 8 9 10 } 0 read print
```
### Replace
```python
{ 1 2 3 4 5 6 7 8 9 10 } "Hello World!" 0 replace print
```
### Remove
```python
{ 1 2 3 4 5 6 7 8 9 10 } 0 remove print
```
### Len
```python
{ 1 2 3 4 5 6 7 8 9 10 } len print
```

## If Statement
```pascal
if true do
    "Hello World" print
end
```
```pascal
if false do
    "Hello World" print
else
    "Hello John Doe" print
end
```
```pascal
if false do
    "Hello World" print
elif true
    "Hello John Doe" print
end
```
```pascal
if true false || do
    "Hello World" print
end
```
```pascal
if true true && do
    "Hello World" print
end
```
```pascal
2 2 == print
2 3 != print
2 3 < print
3 2 > print
2 3 <= print
3 2 >= print
```
```pascal
11 -> N

if N { 20 30 11 42 28 91 } in do
    "Hello World!" print
end
```

## For loop

### Loop 100 times
```pascal
0 for dup 100 <=  do
    dup print
    inc
end
```

### Break
```pascal
0 for dup 100 <= do
    dup print
    if dup 10 == do
        break
    end
end
```

## Error handling
`StackIndexError`
`TypeError`
`ImportError`
`NameError`
`AssertionError`
`IndexError`
```python
try 
    dup
except StackIndexError do
    "Error..." print
except IndexError do
    "Error..." print
except NameError do
    "Error..." print
end
```

## Assertions
```python
false assert "assertion error..."

# AssertionError:1:7: assertion error...
```

## Import
```python
import "main.tsp"
```

## Built-in Words

### Dup
```pascal
"Hello World" dup print print
```
`dup` はスタックの一番上の値を複製します。 ( a -- a a )

### Drop
```pascal
"Hello World" "T# Programming Language" drop print
```
`drop` スタックの一番上の値をスタックから消します。 ( a --  )

### Rot
```python
1 2 3 rot print print print
```
`rot` スタックの値上から三つを回転させます。 ( a b c -- b c a )

### Swap
```pascal
1 2 swap print print
```
`swap` スタックの一番めと二番目の値を入れ替えます。 ( a b -- b a )

### Over
```python
1 2 over print print print
```
`over` スタックの二番目の値を複製してスタックに入れます。 ( a b -- a b a )

### Inc
```python
1 inc print
```
`inc` スタックの一番上の値をインクリメントします。他の言語でいう`++`です。

### Dec
```python
10 dec print
```
`dec` スタックの一番上の値をデクリメントします。他の言語でいう`--`です。

### input
```python
input print
```

### Exit
```python
"Hello World"
exit
print
```
`exit` プログラムを止めます。

### PrintS
```python
1 2 "Hello World!"

printS

# stack length  
#     ↓ 
#    <3>  1 2 'Hello World' <- top
```
`printS` print all stack values. 'printS' won't drop stack value after print.

## Examples
### FizzBuzz
```pascal
1 
for dup 100 <= do
    if dup 15 % 0 == do
        "FizzBuzz" print
    elif dup 3 % 0 == do
        "Fizz" print
    elif dup 5 % 0 == do
        "Buzz" print
    else
        dup print
    end
    inc
end drop
```

### Factorial
```pascal
block Factorial do
    -> n
    1 -> x
    for n 1 >= do
        x n * -> x
        n 1 - -> n
    end
    x
end

5
call Factorial
print
```

### Bubble Sort
```pascal
{ 19 13 6 2 18 8 1 4 11 9 100 30 4 } -> arr arr print

arr len -> length

0 for dup length <= do
    0 for dup length 1 - < do
        dup -> j
        j 1 + -> i
        if arr j read arr i read > do
            arr j read -> x
            arr i read -> y
            arr y j replace dup -> arr
            x i replace -> arr
        end 
        inc
    end drop
    inc
end drop

arr print
```

### Fibonacci
```pascal
10000 -> n

0 1 for over n < do
  over puts " " puts
  swap over +
end
drop drop

"" print
```
