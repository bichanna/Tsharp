# T# ドキュメント

## Introduction
T＃は、ソフトウェアを構築するために設計されたスタックベースのプログラミング言語です。
T＃はPorth、Forthに似ています。

## Install & Run
```bash
$ git clone https://github.com/Tsharp-lang/Tsharp
$ cd tsharp
$ go build main.go
$ ./main examples/main.t#
or
$ ./main.exe examples/main.t#
```

## Hello World
```pascal
push "Hello 世界!" print
```

'push' は値をスタックにプッシュします。
'print' スタックの一番上の要素を print してスタックから削除します。

## コメント
```python
# Sample comment
```

## インポート
```python
import "main.t#"
```

## Block
```pascal
block main do
    push "Hello 世界!" print
end

call main
```

'block' は他の言語でいう関数みたいなもの


## If文
```pascal
if push false do
    push "Hello 世界!" print
else
    push "Hello 世界 else body!" print
end

push 10 push 10 == print
push 20 push 10 != print
push 2 push 10 < print
push 10 push 2 > print
```

## Dup
```pascal
push "Hello World" dup print print
```
'dup' スタックの一番上の要素を複製してスタックに入れます。

## Drop
```pascal
push "Hello 世界!" push "T# Programming Language" drop print
```
'drop' はスタックの一番上の要素を削除します。

## PrintS
```python
push 1 push 2 push [1,2,3,4,["a","b","c"]]

printS

# stack length  
#     ↓ 
#    <3>  1 2 [1, 2, 3, 4, ["a", "b", "c"]] <- top
```
'printS' スタックの中身を全てprint。printした値はスタックからdropされません。

## For loop
```pascal
for push true do
    push "Hello 世界!" print
    break
end
```

## 計算
```pascal
push 34 push 35 + print

push 100 push 40 - print

push 200 push 5 / print

push 10 push 2 * print
```

## 変数
```pascal
push 10 -> x drop

push x -> y drop

push y print
```

## Type
```python
push int # 12345
push string # "Hello World!"
push bool # true false
push type # int string bool type
```

## Typeof
```python
push "Hello World" dup typeof print
```

## Rot
```python
push 1 push 2 push 3 rot print print print
```
'rot' は、上位3つのスタック要素を回転します

## Over
```python
push 1 push 2 over print print print
```
'over' はスタックの一番下にある要素をコピーします

## 文字列結合
```python
push "Hello " push "World!" + print 
```

## インクリメント
```python
push 1 inc print
```

## デクリメント
```python
push 10 dec print
```

## Exit
```python
push "Hello World"
exit
print
```
'exit' はプログラムを強制終了させます。

## リスト
```python
push ["T#", "Ruby", "Python", "C", "Go", "Julia"] dup print

push "V" append dup print

push ["HTML"] append dup print

push [1, 2, 3] append[7]

push "Hello World!" append[7][1]

print
```

## FizzBuzz
```pascal
push 1
for dup push 101 < do
    if dup push 3 % push 0 == do
        if dup push 15 % push 0 == do
            push "FizzBuzz" print
        else
            push "Fizz" print
        end
    else
        if dup push 5 % push 0 == do
            push "Buzz" print
        else
            dup print
        end
    end
    inc
end drop
```
